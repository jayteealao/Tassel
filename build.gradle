// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        compose_version = '1.2.1'
        kotlin_version = '1.7.10'
        coroutines_version = '1.5.2'
        accompanist_version = '0.22.0-rc'
        room_version = '2.4.3'

        // https://github.com/JLLeitschuh/ktlint-gradle/releases
        ktlintPluginVersion = "10.2.0"

        // https://github.com/detekt/detekt/releases
        detektVersion = "1.19.0"

        // Source: https://developer.android.com/jetpack/androidx/releases/lifecycle#2.6.0-alpha01
        lifecycle_version = "2.6.0-alpha01"
    }
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
//        maven { url 'https://www.jitpack.io'}
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath 'com.google.dagger:hilt-android-gradle-plugin:2.43.1'

        classpath "org.jlleitschuh.gradle:ktlint-gradle:$ktlintPluginVersion"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detektVersion"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

subprojects {

    apply from: "../buildscripts/ktlint.gradle"
    apply from: "../buildscripts/detekt.gradle"

    repositories {
        google()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            if (project.findProperty("myapp.enableComposeCompilerReports") == "true") {
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:reportsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics"
                ]
                freeCompilerArgs += [
                        "-P",
                        "plugin:androidx.compose.compiler.plugins.kotlin:metricsDestination=" +
                                project.buildDir.absolutePath + "/compose_metrics"
                ]
            }
        }
    }

}

task clean(type: Delete) {
    delete rootProject.buildDir
}

//tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
//    kotlinOptions {
//        freeCompilerArgs += "-Xcontext-receivers"
//    }
//}